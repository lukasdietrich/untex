# See <https://github.com/pointlander/peg>

package main

type Grammar Peg {
    *Compiler
    StringStack
}

Start       <- Meta? { p.Begin() } Body { p.End() } !.

Meta        <- '---' Spacing Newline
               ( <[[a-z]]+> ':' Spacing { p.Push(text) }
                 <Text*> Newline        { p.SetMeta(p.Pop(), text) }
               / Spacing Newline
               )*
               '---' Spacing Newline

Body        <- ( Import
               / Headline
               / List
               / Latex
               / Paragraph
               )                        { p.NewLine() } 
               (Newline Body)?

Import      <- '@import('
               <(![\r\n)] .)+>          { p.Import(text) }
               ')'

Headline    <- ( SubSubSect 
               / SubSect 
               / Section
               )

Section     <- '#'   Spacing <Text+>    { p.AddSection(text, 0) }
SubSect     <- '##'  Spacing <Text+>    { p.AddSection(text, 1) }
SubSubSect  <- '###' Spacing <Text+>    { p.AddSection(text, 2) }

List        <- ListItem                 { p.EndAllBlocks() }
ListItem    <- Tabs
               ( ListUnorder
               / ListOrdered 
               / ListNamed
               )
               Spacing Inline*          { p.NewLine() }
               (Newline ListItem)?

ListUnorder <- '-'                      { p.AddListItem("itemize", "") }
ListOrdered <- [0-9]+ '.'               { p.AddListItem("enumerate", "") }
ListNamed   <- <[[a-z0-9]]+ ')'>        { p.AddListItem("description", text) }

Tabs        <- { p.tabs = 0 } Tab*
Tab         <- ( '    ' 
               / (' '? ' '? ' '? [\t])
               )                        { p.tabs++ }

Latex       <- '%%%' Spacing Newline
               <(!'%%%' .)*>            { p.AddLatex(text) }
               '%%%' Spacing

Paragraph   <- Inline*

Inline      <- ( IMathBlock
               / IMath
               / IEmph
               / IBold
               / ILink
               / <Text>                 { p.AddText(text) }
               )

IMathBlock  <- <'$$' (!'$' Text)+ '$$'> { p.AddLatex(text) }
IMath       <- <'$' (!'$' Text)+ '$'>   { p.AddLatex(text) }
IEmph       <- '/' <(!'/' Text)+> '/'   { p.AddEmph(text) }
IBold       <- '*' <(!'*' Text)+> '*'   { p.AddBold(text) }
ILink       <- '[' <(!']' Text)+> ']'   { p.Push(text) }
               '(' <(!')' Text)+> ')'   { p.AddLink(p.Pop(), text) }

Text        <- (!Newline .)
Newline     <- '\r'? '\n'
Spacing     <- Whitespace*
Whitespace  <- [ \t]
